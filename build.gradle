plugins {
    id 'com.diffplug.gradle.spotless' version '3.27.2'
    id 'java-library'
    id 'signing'
    id 'maven-publish'
}

group = 'org.seasar.doma'
version = '2.28.1-SNAPSHOT'
ext.dependentVersion = '2.28.0'
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")
ext.encoding = 'UTF-8'
sourceCompatibility = jdkVersion
targetCompatibility = jdkVersion

spotless {
    java {
        googleJavaFormat('1.7')
    }
}

compileJava {
    doFirst {
        ant.replaceregexp(
            match: /(private static final String VERSION = ")[^"]*(")/,
            replace: "\\1${version}\\2",
            encoding: encoding, 
            flags: 'g') {
                fileset(dir: '.') {
                    include(name: '**/Artifact.java')
                }
            }
        ant.replaceregexp(
            match: /('org.seasar.doma:doma-gen:)[^']*(')/,
            replace: "\\1${version}\\2",
            encoding: encoding, 
            flags: 'g') {
                fileset(dir: 'docs') {
                    include(name: '**/*.rst')
                }
            }
    }
    options.encoding = encoding
}

compileTestJava {
    options.encoding = encoding
    options.compilerArgs = ['-proc:none']
}

javadoc {
    options.encoding = encoding
    options.charSet = encoding
    options.docEncoding = encoding
    exclude '**/internal/**'
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Doma-Gen', 'Implementation-Version': archiveVersion
    }
}

repositories {
    mavenCentral()
    mavenLocal()
    maven {url 'https://oss.sonatype.org/content/repositories/snapshots/'}
}

dependencies {
    annotationProcessor "org.seasar.doma:doma:${dependentVersion}"
    implementation "org.seasar.doma:doma:${dependentVersion}"
    implementation 'org.freemarker:freemarker:2.3.20'
    implementation 'org.apache.ant:ant:1.9.3'
    testImplementation 'junit:junit:4.11'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = group
            artifactId = project.name
            version = version
            from components.java

            pom {
                name = project.name
                description = 'Code generator for Doma'
                url = projectUrl
                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'nakamura-to'
                        name = 'Toshihiro Nakamura'
                        email = 'toshihiro.nakamura@gmail.com'
                    }
                }
                scm {
                    connection = "scm:git:${githubUrl}"
                    developerConnection = "scm:git:${githubUrl}"
                    url = projectUrl
                }
            }
            repositories {
                maven {
                    url = isReleaseVersion ? sonatypeUrl : sonatypeSnapshotUrl
                    credentials {
                        username = sonatypeUsername
                        password = sonatypePassword
                    }
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

build.dependsOn publishToMavenLocal

apply from:'eclipse.gradle'